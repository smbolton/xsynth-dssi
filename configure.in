dnl Process this file with autoconf to produce a configure script.

AC_INIT(Xsynth-DSSI, 0.1.1, musound-at-jps-dot-net)

AC_CONFIG_SRCDIR(src/xsynth-dssi.c)
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_ENABLE_STATIC(no)
AC_ENABLE_SHARED(yes)
AC_LIBTOOL_DLOPEN
dnl AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_C_BIGENDIAN

dnl Check for GTK+ and PHAT
AC_ARG_WITH(gtk2, AC_HELP_STRING([--with-gtk2], [use GTK+ 2.0]),
            [ if test $withval = "yes"; then try_gtk2=yes ;
              else try_gtk2=no ; fi ], try_gtk2=yes)
AC_ARG_WITH(phat, AC_HELP_STRING([--with-phat], [use the PHAT GTK+ widget library]),
            [ if test $withval = "yes"; then try_phat=yes ;
              else try_phat=no ; fi ], try_phat=yes)
with_gtk=no
with_phat=no
dnl First look for GTK+ 2.x, unless '--without-gtk2' was specified.
if test "x${try_gtk2}" = 'xyes'; then
    AM_PATH_GTK_2_0(2.0.0, with_gtk='yes (version 2.0)', with_gtk=no)
    if test "x${with_gtk}" != 'xno' -a "x${try_phat}" = 'xyes'; then
        PKG_CHECK_MODULES(PHAT, phat, with_phat=yes, with_phat=no)
        if test "x${with_phat}" = 'xyes'; then
            AC_DEFINE(HAVE_PHAT, 1, [Define this if you have the PHAT GTK+ widget library.])
            GTK_CFLAGS="$GTK_CFLAGS $PHAT_CFLAGS"
            GTK_LIBS="$GTK_LIBS $PHAT_LIBS"
        fi
    fi
fi
dnl  No GTK+ 2.x found, look for GTK+ 1.2 instead
if test "x${with_gtk}" = 'xno'; then
    AM_PATH_GTK(1.2.0, with_gtk='yes (version 1.2)', AC_MSG_ERROR([error: no version of GTK+ found]))
fi
echo "GTK support: $with_gtk"
echo "PHAT support: $with_phat"

dnl Require DSSI and liblo
PKG_CHECK_MODULES(MODULE, dssi >= 0.4 liblo >= 0.12)

dnl Use lotsa flags if we have gcc.
dnl !FIX! the '-finline-limit=5000' is apparently a problem for gcc 2.9x?
CFLAGS="$CFLAGS $MODULE_CFLAGS"
LDFLAGS="$LDFLAGS $MODULE_LIBS"
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
  case " $CFLAGS " in
  *[\ \	]-O[1-9][\ \	]*) ;;
  *) CFLAGS="$CFLAGS -O2" ;;
  esac
  case " $CFLAGS " in
  *[\ \	]-ffast-math[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -fomit-frame-pointer -funroll-loops -finline-functions -ffast-math -finline-limit=5000 -Winline" ;;
  esac
fi
changequote([,])dnl

AC_OUTPUT([
Makefile
src/Makefile
])

